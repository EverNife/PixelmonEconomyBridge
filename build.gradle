buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"

group 'br.com.finalcraft'
version '1.1.5'

targetCompatibility = 1.8
sourceCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    jcenter()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.petrus.dev/public" }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'br.com.finalcraft:EverNifeCore:2.0.4'

    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'br.com.finalcraft:FinalEconomy:1.0.3'

    implementation project(':modules:Commons')
    implementation fileTree(dir: 'libs', include: ['*.jar']) // Include all .jar files in the 'libs' directory

    //Annotations
    compileOnly 'org.jetbrains:annotations:23.0.0'

    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.24'
}

// Define your projects with their respective JAVA_HOME versions
def projectsWithJava = [
        'modules/v1_12_2': System.getenv('JAVA_HOME'), //Java 8
        'modules/v1_16_5': System.getenv('JAVA_HOME'), //Java 8
        'modules/v1_20_2': System.getenv('JAVA_HOME'), //Java 8
        'modules/v1_21_1': 'C:\\Program Files\\Eclipse Adoptium\\jdk-21.0.6.7-hotspot\\',
]

task buildAndMoveJars {
    doLast {
        def jarFiles = []

        projectsWithJava.each { project, javaHome ->
            println("[PROJECT_COMPAT] Building project: ${project} with JAVA_HOME=${javaHome}")

            def gradlewBat = System.getProperty('os.name').toLowerCase().contains('windows')
                    ? 'gradlew.bat'
                    : 'gradlew'

            def buildResult = exec {
                // Use the Gradle Wrapper in the project directory
                workingDir project
                commandLine gradlewBat, 'build'
                environment 'JAVA_HOME', javaHome
                environment 'PATH', "${javaHome}/bin" + File.pathSeparator + System.getenv("PATH")
                ignoreExitValue = true
            }

            if (buildResult.exitValue == 0) {
                println("[PROJECT_COMPAT] Build Completed")

                def projectName = project.substring(project.lastIndexOf('/') + 1)
                def jarName = "${projectName}-COMPAT.jar"
                def jarFile = file("$project/build/libs/${jarName}")

                println("[PROJECT_COMPAT] Moving jar '${jarName}' to 'libs' directory")

                def tempDir = file("libs")
                tempDir.mkdirs()
                def targetFile = file("${tempDir}/${jarName}")
                jarFile.renameTo(targetFile)

                //Add to jarFiles list
                jarFiles.add(targetFile)
            } else {
                // Print an error message if the build failed
                println "[PROJECT_COMPAT] Build failed for project: $project"
            }
        }
    }
}

clean {
    doLast {
        // Delete generated COMPAT JARs
        fileTree(dir: 'libs', include: '**/*-COMPAT.jar').each { file ->
            file.delete()
        }

        // Clean each project with its specific JAVA_HOME
        projectsWithJava.each { project, javaHome ->
            println("[PROJECT_COMPAT] Cleaning project: ${project} with JAVA_HOME=${javaHome}")

            def gradlewBat = System.getProperty('os.name').toLowerCase().contains('windows')
                    ? 'gradlew.bat'
                    : 'gradlew'

            def cleanResult = exec {
                // Use the Gradle Wrapper in the project directory
                workingDir project
                commandLine gradlewBat, 'clean'
                environment 'JAVA_HOME', javaHome
                environment 'PATH', "${javaHome}/bin" + File.pathSeparator + System.getenv("PATH")
                ignoreExitValue = true
            }

            // Check if the build was successful
            if (cleanResult.exitValue == 0) {
                println("[PROJECT_COMPAT] Clean Completed")
            } else {
                // Print an error message if the build failed
                println "[PROJECT_COMPAT] Clean failed for project: $project"
            }
        }
    }
}

shadowJar {
    archiveName = "${baseName}-${version}.${extension}"
}

jar {
    enabled = false //Disable default jar, only shadow jar will be created
    dependsOn(buildAndMoveJars)
    dependsOn(shadowJar { classifier = null })
}